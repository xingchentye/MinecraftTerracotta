plugins {
    id 'net.neoforged.moddev' version '2.0.139'
}

base {
    archivesName = mod_id
}

neoForge {
    version = project.neo_version
    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation project(':ender_core')
    implementation "org.java-websocket:Java-WebSocket:1.5.4"
    implementation "org.apache.commons:commons-compress:1.26.0"
    localRuntime "org.java-websocket:Java-WebSocket:1.5.4"
    localRuntime "org.apache.commons:commons-compress:1.26.0"
}

tasks.named('jar') {
    archiveFileName = "${mod_name}-v${mod_version}-neoforge.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(project(':ender_core').sourceSets.main.output)
    from({
        configurations.runtimeClasspath.files.findAll {
            it.name.contains("Java-WebSocket")
        }.collect { it.isDirectory() ? it : zipTree(it) }
    })
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}
